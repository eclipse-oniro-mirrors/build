# Copyright 2013 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/clang/clang.gni")
import("//build/config/ohos/config.gni")
import("//build/rust/rustc_toolchain.gni")
import("//build/toolchain/gcc_toolchain.gni")

declare_args() {
  # Whether unstripped binaries, i.e. compiled with debug symbols, should be
  # considered runtime_deps rather than stripped ones.
  ohos_unstripped_runtime_outputs = true
  ohos_extra_cflags = ""
  ohos_extra_cppflags = ""
  ohos_extra_cxxflags = ""
  ohos_extra_asmflags = ""
  ohos_extra_ldflags = ""
}

# The ohos clang toolchains share most of the same parameters, so we have this
# wrapper around gcc_toolchain to avoid duplication of logic.
#
# Parameters:
#  - toolchain_root
#      Path to cpu-specific toolchain within the ndk.
#  - sysroot
#      Sysroot for this architecture.
#  - lib_dir
#      Subdirectory inside of sysroot where libs go.
#  - binary_prefix
#      Prefix of compiler executables.
template("ohos_clang_toolchain") {
  gcc_toolchain(target_name) {
    assert(defined(invoker.toolchain_args),
           "toolchain_args must be defined for ohos_clang_toolchain()")
    toolchain_args = invoker.toolchain_args
    toolchain_args.current_os = "ohos"
    rust_abi_target = invoker.rust_abi_target

    # Output linker map files for binary size analysis.
    enable_linker_map = true

    ohos_libc_dir =
        rebase_path(invoker.sysroot + "/" + invoker.lib_dir, root_build_dir)
    libs_section_prefix = "${ohos_libc_dir}/Scrt1.o"
    libs_section_prefix += " ${ohos_libc_dir}/crti.o"
    libs_section_postfix = "${ohos_libc_dir}/crtn.o"

    if (invoker.target_name == "ohos_clang_arm") {
      abi_target = "arm-linux-ohos"
    } else if (invoker.target_name == "ohos_clang_arm64") {
      abi_target = "aarch64-linux-ohos"
    } else if (invoker.target_name == "ohos_clang_x86_64") {
      abi_target = "x86_64-linux-ohos"
    }

    clang_rt_dir =
        rebase_path("${clang_lib_base_path}/${abi_target}", root_build_dir)
    solink_libs_section_prefix = "${ohos_libc_dir}/crti.o"
    solink_libs_section_prefix += " ${clang_rt_dir}/clang_rt.crtbegin.o"
    solink_libs_section_postfix = "${ohos_libc_dir}/crtn.o"
    solink_libs_section_postfix += " ${clang_rt_dir}/clang_rt.crtend.o"

    _prefix = rebase_path("${clang_base_path}/bin", root_build_dir)
    cc = "${_prefix}/clang"
    cxx = "${_prefix}/clang++"
    ar = "${_prefix}/llvm-ar"
    ld = cxx
    readelf = "${_prefix}/llvm-readobj"
    nm = "${_prefix}/llvm-nm"
    if (!is_debug) {
      strip = rebase_path("${clang_base_path}/bin/llvm-strip", root_build_dir)
      use_unstripped_as_runtime_outputs = ohos_unstripped_runtime_outputs
    }
    extra_cflags = ohos_extra_cflags
    extra_cppflags = ohos_extra_cppflags
    extra_cxxflags = ohos_extra_cxxflags
    extra_asmflags = ohos_extra_asmflags
    extra_ldflags = ohos_extra_ldflags

    # Don't use .cr.so for loadable_modules since they are always loaded via
    # absolute path.
    loadable_module_extension = ".so"
    rust_abi_target = invoker.rust_abi_target
    stdlib_path = "${rust_path}/lib/rustlib/${rust_abi_target}/lib"
    std_dylib_path = rebase_path(
            get_label_info("//build/rust:libstd.dylib.so", "target_out_dir"),
            root_build_dir)
    if (rust_abi_target == "arm-unknown-linux-musleabi") {
      stdlibs = "--extern 'noprelude:alloc=$stdlib_path/liballoc-fab3a62110163e76.rlib' --extern 'noprelude:compiler_builtins=$stdlib_path/libcompiler_builtins-8c4ecabd0cb06122.rlib' --extern 'noprelude:core=$stdlib_path/libcore-482b22b5a77239ac.rlib' --extern 'noprelude:panic_unwind=$stdlib_path/libpanic_unwind-591915e967618237.rlib' --extern 'noprelude:proc_macro=$stdlib_path/libproc_macro-874434e28ab7d19d.rlib' --extern 'noprelude:std=$stdlib_path/libstd-e2d7cc624ec4db87.rlib' --extern 'std=$std_dylib_path/libstd.dylib.so' --extern 'test=$stdlib_path/libtest-24f7fe12f298b0ef.rlib'"
      cc_command_args = "--target=${rust_abi_target} -Clinker=$cxx -Clink-arg=-lunwind -Clink-arg=-fuse-ld=lld -Clink-arg=-v -Clink-arg=--target=${abi_target} -Clink-arg=--sysroot=$musl_path -C target-feature=-crt-static -L $musllib -L${clang_base_path}/lib/${abi_target}/c++"
    } else if (rust_abi_target == "aarch64-unknown-linux-musl") {
      stdlibs = "--extern 'noprelude:alloc=$stdlib_path/liballoc-6491e2a3798368e3.rlib' --extern 'noprelude:compiler_builtins=$stdlib_path/libcompiler_builtins-0c0c3d21a4802c2f.rlib' --extern 'noprelude:core=$stdlib_path/libcore-647ac65a75222b99.rlib' --extern 'noprelude:panic_unwind=$stdlib_path/libpanic_unwind-7c64db62d415a989.rlib' --extern 'noprelude:proc_macro=$stdlib_path/libproc_macro-5a8e5dfe87706e3d.rlib' --extern 'noprelude:std=$stdlib_path/libstd-2ffed83c5c6f6122.rlib' --extern 'std=$std_dylib_path/libstd.dylib.so' --extern 'test=$stdlib_path/libtest-29a4bc4bda1b4f47.rlib'"
      cc_command_args = "--target=${rust_abi_target} -Clinker=$cxx -Clink-arg=-lunwind -Clink-arg=-fuse-ld=lld -Clink-arg=-v -Clink-arg=--target=${abi_target} -Clink-arg=--sysroot=$musl_path -C target-feature=-crt-static -L $musllib -L${clang_base_path}/lib/${abi_target}/c++"
    } else if (rust_abi_target == "x86_64-unknown-linux-gnu") {
      stdlibs = "--extern 'noprelude:alloc=$stdlib_path/liballoc-c6c03e024a2f1e46.rlib' --extern 'noprelude:compiler_builtins=$stdlib_path/libcompiler_builtins-b7c79d85cf21a511.rlib' --extern 'noprelude:core=$stdlib_path/libcore-05898138a596088a.rlib' --extern 'noprelude:panic_unwind=$stdlib_path/libpanic_unwind-f568c570fff954b1.rlib' --extern 'noprelude:proc_macro=$stdlib_path/libproc_macro-8f8af17ceb8d77cc.rlib' --extern 'noprelude:std=$stdlib_path/libstd-f30f0c72643db558.rlib' --extern 'std=$std_dylib_path/libstd.dylib.so' --extern 'test=$stdlib_path/libtest-8d62c2c8a15c5ce5.rlib'"
      cc_command_args = "--target=${rust_abi_target} -Clinker=$cxx -Clink-arg=-fuse-ld=lld -Clink-arg=-v -Clink-arg=--target=${abi_target}"
    }
  }
}
