# Copyright (c) 2022 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

declare_args() {
  # Enable the config that variables are automatically initialized by default.
  enable_auto_var_init = false

  # Enable zero initialization config.
  enable_auto_zero_init = false
}

using_auto_var_init = enable_auto_var_init

if (!is_ohos) {
  using_auto_var_init = false
}

assert(
    !using_auto_var_init || is_clang,
    "automatic variable initialization requires setting is_clang = true in 'gn args'")

template("ohos_auto_initialize_config") {
  config(target_name) {
    forward_variables_from(invoker, [ "auto_var_init" ])

    configs = []

    # Currently, only the clang compiler and standard system support automatic variable initialization.
    if (is_clang && is_standard_system) {
      if (defined(auto_var_init)) {
        assert(
            auto_var_init == "pattern" || auto_var_init == "zero" ||
                auto_var_init == "uninit",
            "auto_var_init can only be set to pattern, zero or uninit, for example, auto_var_init = \"pattern\"")

        if (auto_var_init == "pattern") {
          configs += [ "//build/config/secure:auto_var_pattern_init_config" ]
        } else if (auto_var_init == "zero") {
          configs += [ "//build/config/secure:auto_var_zero_init_config" ]
        } else if (auto_var_init == "uninit") {
          configs += [ "//build/config/secure:auto_var_uninit_config" ]
        }
      } else {
        if (enable_auto_zero_init) {
          configs += [ "//build/config/secure:auto_var_zero_init_config" ]
        }
      }
    }
  }
}
